* hydra-helm
This hydra is useful for navigating and invoking features inside Helm buffers.
** helm-persistent-delete-marked
#+BEGIN_SRC emacs-lisp
  (defun helm-persistent-delete-marked ()
    "Kill buffer without quitting helm."
    (interactive)
    (if (equal (cdr (assoc 'name (helm-get-current-source)))
               "Buffers")
        (with-helm-alive-p
          (helm-attrset 'kill-action
                        '(helm-persistent-kill-buffers . never-split))
          (helm-execute-persistent-action 'kill-action))
      (user-error "Only works for buffers")))
#+end_src

** helm-persistent-kill-buffers
#+begin_src emacs-lisp
  (defun helm-persistent-kill-buffers (_buffer)
    (unwind-protect
        (dolist (b (helm-marked-candidates))
          (helm-buffers-persistent-kill-1 b))
      (with-helm-buffer
        (setq helm-marked-candidates nil
              helm-visible-mark-overlays nil))
      (helm-force-update (helm-buffers--quote-truncated-buffer
                          (helm-get-selection)))))
#+end_src

** hydra-helm
#+begin_src emacs-lisp
  (defhydra hydra-helm (:hint nil :color pink)
    "
    ^^^^^^^^^^^^                                                                 ╭──────┐
     ^^Select   ^^Ends  ^^Sources  ^Other      ^^Mark             ^^Do^          │ Helm │
    ╭──^───^────^───^──────^─^──────^─^──────^─^────────────────^─^──────────────┴──────╯
        _k_      _V_       _K_       _p_   [_SPC_] mark         [_._] view
        ^↑^      ^↑^       ^↑^       ^↑^    [_t_]  toggle all   [_f_] follow: %(helm-attr 'follow)
        ^ ^      ^ ^       ^ ^       _c_    [_u_]  unmark all   [_y_] yank selection
        ^↓^      ^↓^       ^↓^       ^↓^    [_d_]  delete       [_w_] toggle windows
        _j_      _v_       _J_       _n_    ^ ^
    ^^^^^^^^^^                                                          _<f19>_ to close
    ^^^^^^^^^^^^────────────────────────────────────────────────────────────────────────
            "
    ("<f19>" nil "quit")
    ("j" helm-next-line)
    ("k" helm-previous-line)
    ("V" helm-beginning-of-buffer)
    ("v" helm-end-of-buffer)
    ("J" helm-next-source)
    ("K" helm-previous-source)
    ("p" helm-scroll-other-window-down)
    ("n" helm-scroll-other-window)
    ("c" helm-recenter-top-bottom-other-window)
    ("SPC" helm-toggle-visible-mark)
    ("t" helm-toggle-all-marks)
    ("u" helm-unmark-all)
    ("." helm-execute-persistent-action)
    ("d" helm-persistent-delete-marked)
    ("y" helm-yank-selection)
    ("w" helm-toggle-resplit-and-swap-windows)
    ("f" helm-follow-mode)
    ("RET" helm-maybe-exit-minibuffer))

  (add-to-list 'hera-hydras '(minibuffer-inactive-mode hydra-helm/body))
#+END_SRC

